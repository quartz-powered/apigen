apply plugin: 'groovy'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'org.quartzpowered'
version = '1.0-SNAPSHOT'

configurations {
    provided
    compile.extendsFrom(provided)
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    provided 'org.projectlombok:lombok:1.16.4'

    compile 'org.ow2.asm:asm-debug-all:5.0.4'
    compile 'com.sun.codemodel:codemodel:2.6'
}

task apiJar(type: Jar) {
    classifier 'api'
    from sourceSets.main.output
    include 'org/quartzpowered/apigen/Expose.class'
}

artifacts {
    archives apiJar
}

publishing {
    repositories {
        maven {
            url 'http://repo.quartzpowered.org/content/repositories/' + (project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases')
            credentials {
                username 'deployment'
                password System.env['maven_pass']
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact apiJar
        }
    }
}